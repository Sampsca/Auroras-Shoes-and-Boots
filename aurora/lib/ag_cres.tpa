//VERSION ~v5.2.4 (2020-12-19)~ 

ACTION_FOR_EACH ~rnt~ IN ~rndequip~ ~rndmagic~ ~rndscrol~ ~rndtreas~ ~rndtres~ ~rndwep~ BEGIN
  ACTION_IF FILE_EXISTS_IN_GAME ~%rnt%.2da~ BEGIN
    COPY_EXISTING ~%rnt%.2da~ ~override~
      t0 = 0 //Gold instances found
      COUNT_2DA_COLS cl
      READ_2DA_ENTRIES_NOW agrnd cl
      FOR (wr = 0; wr < agrnd; wr += 1) BEGIN
        FOR (wc = 1; wc < cl;  wc += 1) BEGIN
          READ_2DA_ENTRY_FORMER agrnd wr wc t1
          PATCH_IF (~%t1%~ STRING_EQUAL ~*~ = 1) BEGIN //Fix * value to 1 gold
            t0 += 1 //Increment gold instances
            SET_2DA_ENTRY_LATER agrnt wr wc 1
          END ELSE BEGIN
            PATCH_IF (~%t1%~ STRING_MATCHES_REGEXP ~^[0-9]+~ = 0) BEGIN
              t0 += 1 //Increment gold instances
              PATCH_IF cg = 2 BEGIN //If raising
                t2 = t1 * nm / dn
              END ELSE BEGIN //If lowering
                t2 = t1 * 100 / dn * nm / 100
              END
              PATCH_IF t2 < 1 BEGIN //Check gold validity
                t2 = 1
              END ELSE BEGIN
                PATCH_IF t2 > 256 BEGIN
                  t2 = 256
                END
              END
              SET_2DA_ENTRY_LATER agrnt wr wc t2
            END
          END
        END
      END
      PATCH_IF t0 > 0 BEGIN
        SET_2DA_ENTRIES_NOW agrnt cl
        PRETTY_PRINT_2DA
      END
    BUT_ONLY
  END
END

<<<<<<<< .../aurora-inlined/ag_cres.txt
CRE	Name	OrigGold	NewGold
>>>>>>>>

COPY ~.../aurora-inlined/ag_cres.txt~ ~aurora/batchlog~

PRINT @382 //This may take some time ...
SILENT
COPY_EXISTING_REGEXP GLOB ~^.+\.cre$~ ~override~
  LAUNCH_PATCH_FUNCTION FJ_CRE_VALIDITY RET valid = valid END
  PATCH_IF valid BEGIN
    READ_LONG 0x1c gd //Gold
    PATCH_IF gd > 3 BEGIN
      READ_STRREF 0x8 nn //Name
      PATCH_IF cg = 2 BEGIN //If raising
        ng = gd * nm / dn
      END ELSE BEGIN //If lowering
        ng = gd / dn * nm
      END
      WRITE_LONG 0x1c ng
      INNER_ACTION BEGIN
        APPEND_OUTER ~aurora/batchlog/ag_cres.txt~ ~%SOURCE_RES%	%nn%	%gd%	%ng%~
      END
    END
  END
BUT_ONLY
VERBOSE
